#!groovy
import org.jetbrains.intellij.tasks.RunPluginVerifierTask


// The above triggers groovy syntax highlighting in vim

plugins {
  id "org.jetbrains.kotlin.jvm" version "1.3.70"
  id "org.jetbrains.intellij" version "0.6.5"
  id "org.sonarqube" version "3.0"
  id "java"
  id "com.github.hierynomus.license" version "0.15.0"
  id "com.jfrog.artifactory" version "4.11.0"
  id 'com.google.protobuf' version "0.8.10"
}

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

group = 'org.sonarsource.sonarlint.intellij'
description = 'SonarLint for IntelliJ IDEA'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

intellij {
  version 'IC-2020.1.3'
  pluginName 'sonarlint-intellij'
  updateSinceUntilBuild false
  plugins = ['java']
}

runPluginVerifier {
  ideVersions = ['IC-2018.3.6', intellij.version]
  failureLevel EnumSet.complementOf(
          EnumSet.of(
                  // these are the only issues we tolerate
                  RunPluginVerifierTask.FailureLevel.DEPRECATED_API_USAGES,
                  RunPluginVerifierTask.FailureLevel.EXPERIMENTAL_API_USAGES,
                  RunPluginVerifierTask.FailureLevel.NOT_DYNAMIC,
                  RunPluginVerifierTask.FailureLevel.OVERRIDE_ONLY_API_USAGES
          )
  )
}

protobuf {
  // Configure the protoc executable
  protoc {
    // Download from repositories. Must be the same as the one used in sonarlint-core
    artifact = 'com.google.protobuf:protoc:3.9.1'
  }
}

test {
  systemProperty 'sonarlint.telemetry.disabled', 'true'
}

runIde {
  systemProperty 'sonarlint.telemetry.disabled', 'true'
}

repositories {
  jcenter()
  mavenLocal()
  maven {
    url "https://repox.jfrog.io/repox/sonarsource"
    content { excludeGroup("typescript") }
  }
  ivy {
    url "https://repox.jfrog.io/repox/api/npm/npm"
    patternLayout {
      artifact "[organization]/-/[module]-[revision].[ext]"
      metadataSources { artifact() }
    }
    content { includeGroup("typescript") }
  }
}

configurations {
  sqplugins { transitive = false }
  typescript { canBeConsumed = false }
}

dependencies {
  compile "org.sonarsource.sonarlint.core:sonarlint-core:$sonarlintCoreVersion"
  compile 'commons-lang:commons-lang:2.6'
  compileOnly 'com.google.code.findbugs:jsr305:2.0.2'
  // provided by the core:
  //compile 'com.google.protobuf:protobuf-java:3.1.0'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.assertj:assertj-core:3.16.1'
  testImplementation 'org.mockito:mockito-core:2.19.0'
  sqplugins (
    'org.sonarsource.java:sonar-java-plugin:6.9.0.23563@jar',
    'org.sonarsource.javascript:sonar-javascript-plugin:6.7.0.14237@jar',
    'org.sonarsource.php:sonar-php-plugin:3.12.0.6710@jar',
    'org.sonarsource.python:sonar-python-plugin:3.1.0.7619@jar',
    'org.sonarsource.slang:sonar-kotlin-plugin:1.8.1.1804@jar',
    'org.sonarsource.slang:sonar-ruby-plugin:1.8.1.1804@jar',
    'org.sonarsource.html:sonar-html-plugin:3.3.0.2534@jar'
  )
  typescript "typescript:typescript:${typescriptVersion}@tgz"
}

task cleanSQPlugins(type: Delete) {
  delete fileTree('src/main/resources/plugins').include('**/*.jar')
}

task downloadSQPlugins(type: Copy, dependsOn: cleanSQPlugins) {
  outputs.upToDateWhen { false }
  from configurations.sqplugins
  into 'src/main/resources/plugins'
}

prepareSandbox {
  doLast {
    def tsBundlePath = project.configurations.typescript[0]
    copy {
      from tarTree(tsBundlePath)
      excludes = [
              '**/loc/**',
              '**/lib/*/diagnosticMessages.generated.json'
      ]
      into file("$destinationDir/$pluginName")
    }
    file("$destinationDir/$pluginName/package").renameTo("$destinationDir/$pluginName/typescript")
  }
}

classes.dependsOn downloadSQPlugins

sonarqube {
  properties {
    property 'sonar.projectName', 'SonarLint for IntelliJ IDEA'
  }
}

license {
  mapping {
    java = 'SLASHSTAR_STYLE'
    kt = 'SLASHSTAR_STYLE'
  }
  strictCheck true
}

jacocoTestReport {
  afterEvaluate {
    classDirectories.setFrom(files('build/classes/java/main-instrumented'))
  }
  reports {
    xml.enabled true
  }
}

artifactory {
  clientConfig.info.setBuildName('sonarlint-intellij')
  clientConfig.info.setBuildNumber(System.getenv('BUILD_BUILDID'))
  clientConfig.setIncludeEnvVars(true)
  clientConfig.setEnvVarsExcludePatterns('*password*,*PASSWORD*,*secret*,*MAVEN_CMD_LINE_ARGS*,sun.java.command,*token*,*TOKEN*,*LOGIN*,*login*')
  clientConfig.info.addEnvironmentProperty('ARTIFACTS_TO_DOWNLOAD', 'org.sonarsource.sonarlint.intellij:sonarlint-intellij:zip')
  contextUrl = System.getenv('ARTIFACTORY_URL')
  publish {    
    repository {
      repoKey = System.getenv('ARTIFACTORY_DEPLOY_REPO')
      username = System.getenv('ARTIFACTORY_DEPLOY_USERNAME')
      password = System.getenv('ARTIFACTORY_DEPLOY_PASSWORD')      
    }
    defaults {
      properties = [
        'vcs.revision': System.getenv('BUILD_SOURCEVERSION'),
        'vcs.branch': (System.getenv('SYSTEM_PULLREQUEST_TARGETBRANCH') ?: System.getenv('BUILD_SOURCEBRANCHNAME')),
        'build.name': 'sonarlint-intellij',
        'build.number': System.getenv('BUILD_BUILDID')
      ]
      publishConfigs('archives')
      publishPom = true // Publish generated POM files to Artifactory (true by default)
      publishIvy = false // Publish generated Ivy descriptor files to Artifactory (true by default)
    }
  }  
}

